apply plugin: 'application'

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    implementation "ch.qos.logback:logback-classic:$libLogbackVersion"
    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation "org.codehaus.groovy:groovy-all:3.0.2"

    // you need to ensure by yourself that the following jars are in the lib folder
    // This might be replaced by implementation "net.kieker-monitoring:kieker:${kiekerVersion}" if the jar is in the local maven repository
    implementation fileTree('lib') {
        include "kieker-${kiekerVersion}-aspectj.jar"
    }
}

distTar.enabled=false

// Simplify the JAR's file name
archivesBaseName = 'BookstoreApplication'
version = ''

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = [ '../../resources' ]

mainClassName = 'kieker.examples.monitoring.customTimeSource.BookstoreStarter'

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

task runMonitoring(type: JavaExec) {
	main = mainClassName
	classpath = sourceSets.main.runtimeClasspath
	
	// The path to the javaagent file might be replaced by ${System.properties['user.home']}/.m2/repository/net/monitoring/kieker/${kiekerVersion}/kieker-${kiekerVersion}-aspectj.jar if the jar is in the local maven repository
	jvmArgs = ['-Dkieker.monitoring.writer.filesystem.FileWriter.customStoragePath=monitoring-logs',
			   '-Dkieker.monitoring.timer=kieker.monitoring.timer.CustomTimeSource',
	           "-javaagent:lib/kieker-${kiekerVersion}-aspectj.jar",
			   '-Dorg.aspectj.weaver.showWeaveInfo=true',
			   '-Daj.weaving.verbose=true']
}
