buildscript {
	repositories {
		maven {url "https://plugins.gradle.org/m2/"}
	}
	dependencies {
		classpath "io.freefair.gradle:aspectj-plugin:6.2.0"
	}
}

plugins {
	id 'java'
	id 'java-library'
	id 'eclipse'
	id 'maven-publish'
}

sourceCompatibility = 8
targetCompatibility = 8

// TODO Why must this be applied, while others get included via plugins?
apply plugin: "io.freefair.aspectj.post-compile-weaving"

// The following lines are necessay to create a test jar
project.configurations {
	testArchives.extendsFrom (testCompile)
}

project.task ('jarTest', type:Jar, dependsOn: project.testClasses, description: 'create a jar from the test source set') {
	from project.sourceSets.test.output
	archiveClassifier = 'test'
}

project.artifacts {
	testArchives project.jarTest
}

repositories {
	mavenCentral()
}

configurations {
	ajc
	aspects
	aspectCompile
	compile {
		extendsFrom aspects
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation project(':common')

	implementation "com.rabbitmq:amqp-client:$libAmqpVersion"

	implementation "org.jctools:jctools-core:$libJctoolsVersion"

	ajc "org.aspectj:aspectjtools:$aspectjVersion"
	implementation "org.aspectj:aspectjrt:$aspectjVersion"
	implementation "org.aspectj:aspectjweaver:$aspectjVersion"	// for our custom AspectJ weaver (class: AspectJLoader)

	implementation 'com.github.oshi:oshi-core:6.4.10'

	// https://mvnrepository.com/artifact/org.influxdb/influxdb-java
	implementation 'org.influxdb:influxdb-java:2.24'

	// disl not available via maven
	implementation resolveLibrary('disl-2.0/disl-server.jar')

	implementation "org.tukaani:xz:$libXzVersion"	// compression algorithms used by the file writers

	implementation "org.apache.cxf:cxf-bundle-minimal:$libCxfVersion"
	implementation 'org.apache.commons:commons-compress:1.25.0'

	// https://mvnrepository.com/artifact/org.glassfish.jersey.core/jersey-server
	implementation 'org.glassfish.jersey.core:jersey-server:3.1.3'
	// https://mvnrepository.com/artifact/org.glassfish.jersey.core/jersey-common
	implementation 'org.glassfish.jersey.core:jersey-common:3.1.5'
	// https://mvnrepository.com/artifact/org.glassfish.jersey.core/jersey-client
	implementation 'org.glassfish.jersey.core:jersey-client:3.1.5'

	// https://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
	implementation 'javax.servlet:javax.servlet-api:4.0.1'
	// https://mvnrepository.com/artifact/javax.jms/javax.jms-api
	implementation 'javax.jms:javax.jms-api:2.0.1'

	implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.1'
	implementation 'javax.jws:javax.jws-api:1.1'

	// https://mvnrepository.com/artifact/aopalliance/aopalliance
	implementation 'aopalliance:aopalliance:1.0'
	// https://mvnrepository.com/artifact/org.springframework
	implementation "org.springframework:spring-beans:$springVersion"
	implementation "org.springframework:spring-context:$springVersion"
	implementation "org.springframework:spring-core:$springVersion"
	implementation "org.springframework:spring-web:$springVersion"
	implementation "org.springframework:spring-webmvc:$springVersion"

	// influxdb
	// https://mvnrepository.com/artifact/org.influxdb/influxdb-java
	implementation 'org.influxdb:influxdb-java:2.24'
	// https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
	implementation 'com.squareup.okhttp3:okhttp:4.12.0'

	// testing
	testImplementation "org.hamcrest:hamcrest:$libHamcrestVersion"

	testImplementation 'org.javassist:javassist:3.29.2-GA'

	testImplementation "junit:junit:$libJunitVersion"

	testImplementation 'org.objenesis:objenesis:3.3'

	testImplementation 'org.easymock:easymock:5.2.0'
	testImplementation "org.mockito:mockito-core:$libMockitoVersion"

	// this project depends on the tests of common, e.g., it requires the class AbstractKiekerTest
	testImplementation project (path: ':common', configuration: 'testArchives')
	testImplementation "org.springframework:spring-beans:$springVersion"
	testImplementation "org.springframework:spring-expression:3.$springVersion"

	testImplementation 'org.eclipse.jetty:jetty-server:9.4.28.v20200408'
	testImplementation 'org.eclipse.jetty:jetty-webapp:9.4.28.v20200408'
	testImplementation 'org.eclipse.jetty:apache-jsp:11.0.18'

	testImplementation 'commons-io:commons-io:2.11.0'

	testImplementation project(':common').sourceSets.test.output

	integrationTestImplementation "junit:junit:$libJunitVersion"
}

// publishing
publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'net.kieker-monitoring'
			artifactId = 'monitoring'
			version = version

			from components.java
		}
	}

	repositories {
		maven {
			def mavenUser = "NoMavenUser"
			if (System.env.kiekerMavenUser != null) {
				mavenUser = System.env.kiekerMavenUser
			}
			def mavenPassword = "NoMavenPassword"
			if (System.env.kiekerMavenPassword != null) {
				mavenPassword = System.env.kiekerMavenPassword
			}
			credentials {
				username = mavenUser
				password = mavenPassword
			}

			// Maven central:
			def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
		}
	}
}

task iajc(dependsOn: classes) {
	doLast {
		ant.taskdef(
			resource: 'org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties',
			classpath: configurations.ajc.asPath
		)
		ant.iajc(
			XlintFile: 'aspectjXlint.properties',
			X: 'joinpoints:synchronization',
			source: sourceCompatibility,
			target: targetCompatibility,
			debug: 'true',
			destdir: sourceSets.main.output.classesDir) {
			classpath {
				pathElement(location: configurations.compile.asPath)
				pathElement(location: sourceSets.main.output.classesDir)
			}

			sourceroots {
				pathElement(location: 'src/kieker/monitoring/probe/aspectj')
			}
		}
	}
}

sourceSets {
	jarIntegrationTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output

		compileClasspath += sourceSets.test.output
		runtimeClasspath += sourceSets.test.output

		java {
			srcDirs = [ 'integrationTest' ]
		}
	}
}

configurations {
	jarIntegrationTestImplementation.extendsFrom testImplementation
	jarIntegrationTestRuntimeOnly.extendsFrom runtimeOnly
}

task jarIntegrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.jarIntegrationTest.output.classesDirs
	classpath = sourceSets.jarIntegrationTest.runtimeClasspath
	dependsOn rootProject.aspectJJar
}

def resolveLibrary(String library) {
def libraryDir = rootProject.file(dirLib)
return fileTree(dir: libraryDir, include: library).filter { it.isFile() }
}

// This is necessary to avoid eclipse problems; eclipse does not allow the same project to be imported twice as dependency
eclipse {
	classpath {
		file {
			whenMerged { classpath ->
				classpath.entries.removeAll { entry ->
					entry instanceof org.gradle.plugins.ide.eclipse.model.ProjectDependency
						&& entry.path == '/common'
						&& entry.entryAttributes['test']
				}
			}
		}
	}
}
