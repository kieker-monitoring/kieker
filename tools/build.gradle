plugins {
	id 'java-library'
	id 'eclipse'
	id 'maven-publish'
}

sourceCompatibility = 17
targetCompatibility = 17

// The following lines are necessay to create a test jar
project.configurations {
	testArchives.extendsFrom (testCompile)
}

project.task ('jarTest', type:Jar, dependsOn: project.testClasses, description: 'create a jar from the test source set') {
	from project.sourceSets.test.output
	archiveClassifier = 'test'
}

project.artifacts {
	testArchives project.jarTest
}

dependencies {
	implementation project(':common')
	api project(':analysis')
	api project(':model')
	api project(':monitoring')

	implementation "com.beust:jcommander:${jcommanderVersion}"

	implementation "javax.jms:javax.jms-api:2.0.1"
	implementation "javax.servlet:javax.servlet-api:4.0.1"

	implementation "ch.qos.logback:logback-classic:$libLogbackVersion"
	implementation "org.slf4j:slf4j-api:$libSlf4jApiVersion"

	// PlantUML for generating diagrams / Dependencies for PDF generation
	implementation 'net.sourceforge.plantuml:plantuml:1.2025.2'
	implementation 'org.apache.pdfbox:pdfbox:2.0.27'
	implementation 'org.apache.xmlgraphics:batik-all:1.17' // Complete Batik for PlantUML PDF support
	implementation 'org.apache.xmlgraphics:fop:2.9'

	// Some dependencies for the HTTP connector of the KDB
	api fileTree(dir: rootProject.file('kieker-examples/JavaEEServletContainerExample/jetty/lib'), include: 'jetty-*.jar')

	testImplementation "junit:junit:$libJunitVersion"

	testImplementation project(':common').sourceSets.test.output
	testImplementation project(':analysis').sourceSets.test.output
}

// publishing
publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'net.kieker-monitoring'
			artifactId = 'tools'
			version = version

			from components.java
		}
	}

	repositories {
		maven {
			def mavenUser = "NoMavenUser"
			if (System.env.kiekerMavenUser != null) {
				mavenUser = System.env.kiekerMavenUser
			}
			def mavenPassword = "NoMavenPassword"
			if (System.env.kiekerMavenPassword != null) {
				mavenPassword = System.env.kiekerMavenPassword
			}
			credentials {
				username = mavenUser
				password = mavenPassword
			}

			// Maven central:
			def releasesRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://central.sonatype.com/repository/maven-snapshots/'

			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
		}
	}
}
// This is necessary to avoid eclipse problems; eclipse does not allow the same project to be imported twice as dependency
eclipse {
	classpath {
		file {
			whenMerged { classpath ->
				classpath.entries.removeAll { entry ->
					entry instanceof org.gradle.plugins.ide.eclipse.model.ProjectDependency
						&& entry.path == '/common'
						&& entry.entryAttributes['test']
				}
			}
		}
		containsTestFixtures = true
	}
}
