plugins {
	id 'application'
	id 'eclipse'
}

sourceCompatibility = 11
targetCompatibility = 11

distTar.enabled=false
distZip.enabled=true

mainClassName='kieker.tools.log.replayer.ReplayerMain'

sourceSets.main.resources.srcDirs = [ '../resources' ]

dependencies {
	implementation project(":tools")
	implementation "com.beust:jcommander:${jcommanderVersion}"

	testImplementation project(':common').sourceSets.test.output
	testImplementation project(':monitoring').sourceSets.test.output
	testImplementation project(':analysis').sourceSets.test.output
	testImplementation project(':tools').sourceSets.test.output

	testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
}

task uploadArchives {
	println 'log-replayer skipped'
}

eclipse {
	classpath {
		file {
			whenMerged { classpath ->
				classpath.entries.removeAll { entry ->
					entry instanceof org.gradle.plugins.ide.eclipse.model.ProjectDependency
						&& ['/common', '/tools'].contains(entry.path)
						&& entry.entryAttributes['test']
				}
			}
		}
	}
}
